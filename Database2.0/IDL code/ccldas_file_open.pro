;+
; NAME:
; ccldas_file_open
;
; PURPOSE:
; Opens an hdf5 file generated by the CCLDAS database query system and returns
; a file reference number
;
; CALLING SEQUENCE:
; Result = ccldas_file_open('FILE PATH',info=info)
;
; INPUTS:
; filename: A string with the location of the hdf5 file to be opened.
;
; info (keyword):  A struct with some metadata describing the file
;     info.timestamp:       UNIX timestamp with milliseconds since Jan 1 1970
;     info.version_number:  Integer file version.  Currently only "1" supported.
;
; OUTPUTS:
; Returns an integer file reference number
;
; MODIFICATION HISTORY:
; Written August 2012 by Spenser Burrows
;-

FUNCTION ccldas_file_open,filename,info=info

  file_id=H5F_OPEN(filename);open file reference
  
  info={timestamp:long64(0),version_number:long(0)}
  info_names=STRLOWCASE(tag_names(info))
  info_group_id=H5G_OPEN(file_id,'/')
  
  for i=0,1 do begin
    attribute_id=H5A_OPEN_NAME(info_group_id,info_names[i])
    info.(i)=H5A_READ(attribute_id)
    H5A_close,attribute_id
  endfor
  
  H5G_CLOSE,info_group_id 

  RETURN,file_id
END
